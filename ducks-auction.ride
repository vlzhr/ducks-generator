{-# STDLIB_VERSION 4 #-}
{-# SCRIPT_TYPE ACCOUNT #-}
{-# CONTENT_TYPE DAPP #-}

let incubatorAddress = Address(base58'3PEktVux2RhchSN63DsDo4b4mz4QqzKSeDv')
let breederAddress = Address(base58'3PDVuU45H7Eh5dmtNbnRNRStGwULA7NY6Hb')
let feeAggregator = Address(base58'3P6AobNbcQ3ZjcRY37mJPKN8PP9Giz8kgeY')

let feePercent = 5

func tryGetInteger(key: String) = this.getInteger(key).valueOrElse(0)

func tryGetString(key: String) = this.getString(key).valueOrElse("")

func getAssetGenotype(assetId: ByteVector) = {
  let asset = assetId.toBase58String()

  let incubatorAsset = incubatorAddress.getString(asset).valueOrElse("")
  let breederAsset = breederAddress.getString(asset).valueOrElse("")

  if incubatorAsset != "" then
    incubatorAsset
  else if breederAsset != "" then
    breederAsset
  else
    ""
}

func getBidCancelResults(auctionId: String, bidId: String) = {
  let assetId = this.getStringValue("auction_" + auctionId + "_assetId")

  let bidAuthor = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_author")
  let bidAmount = tryGetInteger("auction_" + auctionId + "_bid_" + bidId + "_amount")
  [
    StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + bidAuthor + "\", \"amount\": \"" + bidAmount.toString() + "\", \"status\": \"cancelled\"}"),
    StringEntry("auction_" + auctionId + "_bid_" + bidId + "_status", "cancelled"),
    DeleteEntry("address_" + bidAuthor + "_auction_" + auctionId + "_bid_" + bidId),
    ScriptTransfer(Address(bidAuthor.fromBase58String()), bidAmount, unit)
  ]
}

@Callable(i)
func initAuction(startPrice: Int, instantPrice: Int, description: String) = {
  let auctionId = i.transactionId.toBase58String()
  let pmt = i.payments[0] # TODO check payments amount
  let assetId = pmt.assetId.value()

  let asset = getAssetGenotype(assetId)

  # TODO check startPrice > 0 and instantPrice > startPrice
  if (asset == "") then {
    throw("You can only sell an NFT-duck created in incubator or breeder")
  } else {
    [
      StringEntry("auction_" + auctionId + "_status", "open"),
      StringEntry("auction_" + assetId.toBase58String() + "_last", auctionId),
      StringEntry("auction_" + auctionId + "_assetGen", asset),
      StringEntry("auction_" + auctionId + "_assetId", assetId.toBase58String()),
      IntegerEntry("auction_" + auctionId + "_startedAt", lastBlock.timestamp),
      StringEntry("auction_" + auctionId + "_owner", i.caller.toString()),
      StringEntry("auction_" + auctionId + "_description", description),
      IntegerEntry("auction_" + auctionId + "_startPrice", startPrice),
      IntegerEntry("auction_" + auctionId + "_instantPrice", instantPrice),
      StringEntry("address_" + i.caller.toString() + "_auction_" + auctionId + "_lockedNFT", assetId.toBase58String())
    ]
  }
}

@Callable(i)
func cancelAuction(auctionId: String) = {
  let auctionOwner = tryGetString("auction_" + auctionId + "_owner")
  if (i.caller.toString() != auctionOwner) then {
    throw("Only allowed to auction owner")
  } else {
    if (tryGetString("auction_" + auctionId + "_status") != "open") then {
      throw("auction is close")
    } else {
      [
        StringEntry("auction_" + auctionId + "_status", "cancelled"),
        DeleteEntry("address_" + auctionOwner + "_auction_" + auctionId + "_lockedNFT"),
        ScriptTransfer(i.caller, 1, this.getStringValue("auction_" + auctionId + "_assetId").fromBase58String())
      ]
    }
  }
}

@Callable(i)
func instantBuy(auctionId: String) = {
  let pmt = i.payments[0].value()
  let amount = pmt.amount
  let assetId = tryGetString("auction_" + auctionId + "_assetId")
  let bidId = i.transactionId.toBase58String()

  let auctionStatus = tryGetString("auction_" + auctionId + "_status")

  if (pmt.assetId != unit) then {
    throw("Auction does only support WAVES payments.")
  } else {
    if (auctionStatus != "open") then {
      throw("Auction is closed. You cannot place a bid.")
    } else {
      let instantPrice = tryGetInteger("auction_" + auctionId + "_instantPrice")
      if (instantPrice > amount) then {
        throw("For instant purchase you need to pay at least " + instantPrice.toString())
      } else {
        let auctionOwner = tryGetString("auction_" + auctionId + "_owner")
        let recordPrice = tryGetInteger(assetId+"_recordPrice")
        let newRecordPrice = max([recordPrice, amount])

        [
          StringEntry("auction_" + auctionId + "_status", "finished"),
          IntegerEntry("auction_" + auctionId + "_finalPrice", amount),
          IntegerEntry(assetId + "_recordPrice", newRecordPrice),
          StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + i.caller.toString() + "\", \"amount\": \"" + amount.toString() + "\", \"status\": \"finished\"}"),
          DeleteEntry("address_" + auctionOwner + "_auction_" + auctionId + "_lockedNFT"),
          ScriptTransfer(i.caller, 1, this.getStringValue("auction_" + auctionId + "_assetId").fromBase58String()),
          ScriptTransfer(addressFromStringValue(auctionOwner), fraction(amount, 100-feePercent, 100), unit),
          ScriptTransfer(feeAggregator, fraction(amount, feePercent, 100), unit)
        ]
      }
    }
  }
}

@Callable(i)
func placeBid(auctionId: String) = {
  let pmt = i.payments[0].value()
  let amount = pmt.amount
  let bidId = i.transactionId.toBase58String()
  let assetId = this.getStringValue("auction_" + auctionId + "_assetId")

  if (pmt.assetId != unit) then {
    throw("Auction does only support WAVES payments")
  } else {
    let minPrice = tryGetInteger("auction_" + auctionId + "_startPrice")
    if (minPrice > amount) then {
      throw("The minimal initial price is set as " + minPrice.toString())
    } else {
      [
        StringEntry("auction_" + auctionId + "_bid_" + bidId + "_author", i.caller.toString()),
        StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + i.caller.toString() + "\", \"amount\": \"" + amount.toString() + "\", \"status\": \"open\"}"),
        IntegerEntry("auction_" + auctionId + "_bid_" + bidId + "_amount", amount),
        StringEntry("address_" + i.caller.toString() + "_auction_" + auctionId + "_bid_" + bidId, assetId),
        StringEntry("auction_" + auctionId + "_bid_" + bidId + "_status", "open")
      ]    
    }
  }
}

@Callable(i)
func cancelBid(auctionId: String, bidId: String) = {

  let assetId = this.getStringValue("auction_" + auctionId + "_assetId")

  let bidAuthor = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_author")
  let bidAmount = tryGetInteger("auction_" + auctionId + "_bid_" + bidId + "_amount")

  if (i.caller.toString() != bidAuthor) then {
    throw("Only allowed to the bid author")
  } else {
    if (tryGetString("auction_" + auctionId + "_bid_" + bidId + "_status") != "open") then {
      throw("bid is not open")
    } else {
      getBidCancelResults(auctionId, bidId)
    }
  }
}

@Callable(i)
func cancelOutdatedBid(auctionId: String, bidId: String) = {
  let auctionStatus = tryGetString("auction_" + auctionId + "_status")
  if (tryGetString("auction_" + auctionId + "_bid_" + bidId + "_status") != "open") then {
    throw("bid is not open")
  } else if (auctionStatus != "cancelled" && auctionStatus != "finished") then {
    throw("this oracle can cancel bids only for canceled or finished auctions")
  } else {
    getBidCancelResults(auctionId, bidId)
  }
}

@Callable(i)
func acceptBid(auctionId: String, bidId: String) = {
  let auctionOwner = tryGetString("auction_" + auctionId + "_owner")
  let bidAuthor = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_author")
  let bidAmount = tryGetInteger("auction_" + auctionId + "_bid_" + bidId + "_amount")
  let bidStatus = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_status")
  let assetId = tryGetString("auction_" + auctionId + "_assetId")

  if (i.caller.toString() != auctionOwner) then {
    throw("Only allowed to auction owner")
  } else {
    if (bidStatus != "open") then {
      throw("Bid was cancelled and cannot be accepted")
    } else {
      let recordPrice = tryGetInteger(assetId+"_recordPrice")
      let newRecordPrice = max([recordPrice, bidAmount])
      [
        StringEntry("auction_" + auctionId + "_bid_" + bidId + "_status", "finished"),
        StringEntry("auction_" + auctionId + "_status", "finished"),
        StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + i.caller.toString() + "\", \"amount\": \"" + bidAmount.toString() + "\", \"status\": \"finished\"}"),
        IntegerEntry("auction_" + auctionId + "_finalPrice", bidAmount),
        IntegerEntry(assetId + "_recordPrice", newRecordPrice),
        DeleteEntry("address_" + bidAuthor + "_auction_" + auctionId + "_bid_" + bidId),
        DeleteEntry("address_" + auctionOwner + "_auction_" + auctionId + "_lockedNFT"),
        ScriptTransfer(i.caller, fraction(bidAmount, 100-feePercent, 100), unit),
        ScriptTransfer(feeAggregator, fraction(bidAmount, feePercent, 100), unit),
        ScriptTransfer(addressFromStringValue(bidAuthor), 1, assetId.fromBase58String())
      ]
    }
  }
}


@Verifier(tx)
func verify() = sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
